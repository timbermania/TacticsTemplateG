#https://ffhacktics.com/wiki/Maps/Mesh
class_name FftMapData
extends Resource

const TILE_SIDE_LENGTH: int = 28
const UNITS_PER_HEIGHT: int = 12
const SCALE: float = 1.0 / TILE_SIDE_LENGTH
const HEIGHT_SCALE: float = UNITS_PER_HEIGHT / (TILE_SIDE_LENGTH * 1.0)
const TEXTURE_SIZE: Vector2i = Vector2i(256, 1024)

const NUM_VERTICIES_PER_TRI: int = 3
const NUM_VERTICIES_PER_QUAD: int = 4
const BYTES_PER_TERRAIN_TILE: int = 8
const TEXTURE_BYTES_PER_TRI: int = 10
const TEXTURE_BYTES_PER_QUAD: int = 12

@export var unique_name: String = "unique_name"
@export var display_name: String = "display_name"
@export var description: String = "description"

var is_initialized: bool = false

@export var file_name: String = "default map file name"
@export var primary_mesh_data_record: MapFileRecord
@export var primary_texture_record: MapFileRecord
@export var other_data_record: MapFileRecord
@export var map_file_records: Array[MapFileRecord] = []

@export var mesh: ArrayMesh
@export var mesh_material: StandardMaterial3D
@export var albedo_texture: Texture2D
@export var albedo_texture_indexed: Texture2D
var st: SurfaceTool = SurfaceTool.new()

@export var num_text_tris: int = 0
@export var num_text_quads: int = 0
@export var num_black_tris: int = 0
@export var num_black_quads: int = 0

@export var text_tri_vertices: PackedVector3Array = []
@export var text_quad_vertices: PackedVector3Array = []
@export var black_tri_vertices: PackedVector3Array = []
@export var black_quad_vertices: PackedVector3Array = []

@export var text_tri_normals: PackedVector3Array = []
@export var text_quad_normals: PackedVector3Array = []

@export var tris_texture_bytes: PackedByteArray = []
@export var quads_texture_bytes: PackedByteArray = []
@export var untextured_polygon_bytes: PackedByteArray = []
@export var textured_polygon_tile_bytes: PackedByteArray = []

@export var tris_uvs: PackedVector2Array = []
@export var quads_uvs: PackedVector2Array = []
@export var tris_palettes: PackedInt32Array = []
@export var quads_palettes: PackedInt32Array = []

@export var texture_palette_bytes: PackedByteArray = []
@export var texture_palettes: PackedColorArray = []
@export var texture_color_indices: PackedInt32Array = []

@export var lighting_and_gradient_bytes: PackedByteArray = []
@export var background_gradient_top: Color = Color.DIM_GRAY
@export var background_gradient_bottom: Color = Color.BLACK
## The three directional lights + ambient decoded out of the FIRST 39 bytes of
## `lighting_and_gradient_bytes` - the gradient above is the last 6 of the same
## block. `MapLighting.unlit()` until `create_map` reads a block.
@export var map_lighting: MapLighting = MapLighting.unlit()

@export var terrain_data_bytes: PackedByteArray = []
@export var map_width: int = 0 # width (x) in tiles
@export var map_length: int = 0 # length (y) in tiles
@export var terrain_tiles: Array[TerrainTile] = []

# texture animations
@export var has_texture_animations: bool = false
@export var texture_anim_instructions_bytes: Array[PackedByteArray] = []
@export var texture_animations_palette_frames: Array[PackedColorArray] = []
@export var palette_animation_bytes: PackedByteArray = []
@export var texture_animations: Array[TextureAnimationData] = []

@export var texture_palette_grayscale_bytes: PackedByteArray = []
@export var mesh_animation_instruction_bytes: PackedByteArray = []

# polygon render flags
@export var unknown_render_bytes: PackedByteArray # 896 bytes long
@export var textured_tris_flags: PackedByteArray # 1024 bytes long for 512 textured triangles
@export var textured_quads_flags: PackedByteArray # 1536 bytes for 768 textured quads
@export var black_tris_flags: PackedByteArray # 128 bytes for 64 untextured triangles
@export var black_quads_flags: PackedByteArray # 512 bytes for 256 untextured quads


static func get_transformed_mesh(
	original_mesh: ArrayMesh, 
	pivot_point: Vector3,
	scale: Vector3 = Vector3.ONE, 
	translation: Vector3 = Vector3.ZERO, 
	rotation_degrees: float = 0.0,
	move_to_positive_quadrant: bool = false,
) -> ArrayMesh:
	var mesh_transform: Transform3D = Transform3D.IDENTITY
	mesh_transform = mesh_transform.translated(-pivot_point)
	mesh_transform = mesh_transform.rotated(Vector3.UP, deg_to_rad(rotation_degrees))
	mesh_transform = mesh_transform.scaled(scale)
	# mesh_transform = mesh_transform.translated(mesh_center)
	var mirror_offset: Vector3 = (scale * -1).clamp(Vector3.ZERO, Vector3.ONE)
	mirror_offset.y = 0
	if move_to_positive_quadrant:
		mesh_transform = mesh_transform.translated(pivot_point.abs() + translation + mirror_offset)
	else:
		mesh_transform = mesh_transform.translated(pivot_point + translation + mirror_offset)

	var surface_arrays: Array = original_mesh.surface_get_arrays(0)
	for vertex_idx: int in surface_arrays[Mesh.ARRAY_VERTEX].size():
		var vertex: Vector3 = mesh_transform * surface_arrays[Mesh.ARRAY_VERTEX][vertex_idx]
		surface_arrays[Mesh.ARRAY_VERTEX][vertex_idx] = vertex

	var custom0_flags: int = FftMapData.transform_custom0(surface_arrays, mesh_transform)

	# reorder verticies so polygon will have correct facing
	var sum_scale: int = roundi(scale.x) + roundi(scale.y) + roundi(scale.z)
	if sum_scale == 1 or sum_scale == -3:
		for idx: int in surface_arrays[Mesh.ARRAY_VERTEX].size() / 3:
			var tri_idx: int = idx * 3
			var temp_vertex: Vector3 = surface_arrays[Mesh.ARRAY_VERTEX][tri_idx]
			surface_arrays[Mesh.ARRAY_VERTEX][tri_idx] = surface_arrays[Mesh.ARRAY_VERTEX][tri_idx + 2]
			surface_arrays[Mesh.ARRAY_VERTEX][tri_idx + 2] = temp_vertex

			var temp_uv: Vector2 = surface_arrays[Mesh.ARRAY_TEX_UV][tri_idx]
			surface_arrays[Mesh.ARRAY_TEX_UV][tri_idx] = surface_arrays[Mesh.ARRAY_TEX_UV][tri_idx + 2]
			surface_arrays[Mesh.ARRAY_TEX_UV][tri_idx + 2] = temp_uv

	var transformed_mesh: ArrayMesh = ArrayMesh.new()
	transformed_mesh.add_surface_from_arrays(Mesh.PRIMITIVE_TRIANGLES, surface_arrays, [], {}, custom0_flags)
	
	return transformed_mesh


## Mirror CUSTOM0 centroid data in surface arrays.
## Returns the CUSTOM0 format flags to pass to add_surface_from_arrays().
static func transform_custom0(surface_arrays: Array, transform: Transform3D) -> int:
	if surface_arrays.size() <= Mesh.ARRAY_CUSTOM0 or surface_arrays[Mesh.ARRAY_CUSTOM0] == null:
		return 0
	var mesh_custom0: PackedFloat32Array = surface_arrays[Mesh.ARRAY_CUSTOM0]
	@warning_ignore("integer_division")
	var num_verticies: int = mesh_custom0.size() / 4
	for vertex_idx: int in range(num_verticies):
		var x_index: int = vertex_idx * 4
		var centroid: Vector3 = Vector3(mesh_custom0[x_index], mesh_custom0[x_index + 1], mesh_custom0[x_index + 2])
		var render_flags: int = roundi(mesh_custom0[x_index + 3])
		var scale: Vector3 = Vector3(transform.basis.x.x, transform.basis.y.y, transform.basis.z.z)
		var new_render_flags: int = 0
		# mirror render flags
		if roundi(scale.x) == -1 and roundi(scale.z) == -1:
			if render_flags & MapData.HiddenDirectionFlag.NORTHEAST != 0:
				new_render_flags |= MapData.HiddenDirectionFlag.SOUTHWEST
			if render_flags & MapData.HiddenDirectionFlag.SOUTHEAST != 0:
				new_render_flags |= MapData.HiddenDirectionFlag.NORTHWEST
			if render_flags & MapData.HiddenDirectionFlag.NORTHWEST != 0:
				new_render_flags |= MapData.HiddenDirectionFlag.SOUTHEAST
			if render_flags & MapData.HiddenDirectionFlag.SOUTHWEST != 0:
				new_render_flags |= MapData.HiddenDirectionFlag.NORTHEAST
		elif roundi(scale.x) == -1:
			if render_flags & MapData.HiddenDirectionFlag.NORTHEAST != 0:
				new_render_flags |= MapData.HiddenDirectionFlag.NORTHWEST
			if render_flags & MapData.HiddenDirectionFlag.SOUTHEAST != 0:
				new_render_flags |= MapData.HiddenDirectionFlag.SOUTHWEST
			if render_flags & MapData.HiddenDirectionFlag.NORTHWEST != 0:
				new_render_flags |= MapData.HiddenDirectionFlag.NORTHEAST
			if render_flags & MapData.HiddenDirectionFlag.SOUTHWEST != 0:
				new_render_flags |= MapData.HiddenDirectionFlag.SOUTHEAST
		elif roundi(scale.z) == -1:
			if render_flags & MapData.HiddenDirectionFlag.NORTHEAST != 0:
				new_render_flags |= MapData.HiddenDirectionFlag.SOUTHEAST
			if render_flags & MapData.HiddenDirectionFlag.SOUTHEAST != 0:
				new_render_flags |= MapData.HiddenDirectionFlag.NORTHEAST
			if render_flags & MapData.HiddenDirectionFlag.NORTHWEST != 0:
				new_render_flags |= MapData.HiddenDirectionFlag.SOUTHWEST
			if render_flags & MapData.HiddenDirectionFlag.SOUTHWEST != 0:
				new_render_flags |= MapData.HiddenDirectionFlag.NORTHWEST
		else:
			new_render_flags = render_flags
		# TODO rotate render flags
		centroid = transform * centroid
		mesh_custom0[x_index] = centroid.x
		mesh_custom0[x_index + 1] = centroid.y
		mesh_custom0[x_index + 2] = centroid.z
		mesh_custom0[x_index + 3] = float(new_render_flags) # keep flag to hide polygon
	surface_arrays[Mesh.ARRAY_CUSTOM0] = mesh_custom0
	# Godot needs explicit format flags for CUSTOM0 in add_surface_from_arrays()
	# RGB_FLOAT = 6, CUSTOM0 format shift = 13
	return (Mesh.ARRAY_CUSTOM_RGBA_FLOAT << Mesh.ARRAY_FORMAT_CUSTOM0_SHIFT)


func _init(map_file_name: String = "") -> void:
	if map_file_name != "":
		file_name = map_file_name


func init_map() -> void:
	var map_gns_data: PackedByteArray = RomReader.get_file_data(file_name)
	init_map_data(map_gns_data)
	is_initialized = true


func init_map_data(gns_bytes: PackedByteArray) -> void:
	map_file_records = get_associated_files(gns_bytes)
	push_warning("MapChunkNodes Mesh File: " + primary_mesh_data_record.file_name)
	push_warning("MapChunkNodes Texture File: " + primary_texture_record.file_name)
	create_map(
		RomReader.get_file_data(primary_mesh_data_record.file_name),
		RomReader.get_file_data(primary_texture_record.file_name),
	)


func create_map(mesh_bytes: PackedByteArray, texture_bytes: PackedByteArray = []) -> void:
	var other_bytes: PackedByteArray = mesh_bytes
	if file_name == "MAP053.GNS": # handle special case
		other_bytes = RomReader.get_file_data(other_data_record.file_name)

	var primary_mesh_data_start: int = mesh_bytes.decode_u32(0x40)
	var texture_palettes_data_start: int = other_bytes.decode_u32(0x44)
	var lighting_data_start: int = other_bytes.decode_u32(0x64)
	var terrain_data_start: int = other_bytes.decode_u32(0x68)
	var texture_animation_instructions_data_start: int = other_bytes.decode_u32(0x6c)
	var palette_animation_frames_data_start: int = other_bytes.decode_u32(0x70)
	var texture_palette_grayscale_data_start: int = other_bytes.decode_u32(0x7c)
	var polygon_render_flags_start: int = other_bytes.decode_u32(0xb0)
	#var primary_mesh_data_end: int = texture_palettes_data_start if texture_palettes_data_start > 0 else 2147483647

	#var primary_mesh_data: PackedByteArray = mesh_bytes.slice(primary_mesh_data_start, primary_mesh_data_end)
	var primary_mesh_data: PackedByteArray = mesh_bytes.slice(primary_mesh_data_start)
	set_mesh_data(primary_mesh_data)

	if texture_palettes_data_start == 0:
		push_warning("No palette data found")
	else:
		var texture_palettes_data_end: int = texture_palettes_data_start + 512
		texture_palette_bytes = other_bytes.slice(texture_palettes_data_start, texture_palettes_data_end)
		texture_palettes = get_texture_palettes(texture_palette_bytes)

	if lighting_data_start == 0:
		push_warning("No lighting data found")
	else:
		# 6 bytes for each directional light color, position, 3 bytes for ambient light color, 6 bytes for gradient colors
		var lighting_data_length: int = 18 + 18 + 3 + 6
		var lighting_data_end: int = lighting_data_start + lighting_data_length
		lighting_and_gradient_bytes = other_bytes.slice(lighting_data_start, lighting_data_end)
		map_lighting = MapLighting.from_lighting_bytes(lighting_and_gradient_bytes)
		set_gradient_colors(lighting_and_gradient_bytes.slice(-6))

	if terrain_data_start == 0:
		push_warning("No terrain data found")
	else:
		var terrain_data_length: int = 2 + (256 * BYTES_PER_TERRAIN_TILE * 2)
		var terrain_data_end: int = terrain_data_start + terrain_data_length
		terrain_data_bytes = other_bytes.slice(terrain_data_start, terrain_data_end)
		terrain_tiles = get_terrain(terrain_data_bytes)
	
	if texture_palette_grayscale_data_start == 0:
		push_warning("No grayscale texture palettes found")
	else:
		texture_palette_grayscale_bytes = other_bytes.slice(texture_palette_grayscale_data_start, texture_palette_grayscale_data_start + 512)

	if polygon_render_flags_start == 0:
		push_warning("No polygon render flags found")
	else:
		unknown_render_bytes = other_bytes.slice(polygon_render_flags_start, polygon_render_flags_start + 896) # 896 bytes long
		textured_tris_flags = other_bytes.slice(polygon_render_flags_start + 896, polygon_render_flags_start + 896 + 1024) # 1024 bytes long for 512 textured triangles
		textured_quads_flags = other_bytes.slice(polygon_render_flags_start + 896 + 1024, polygon_render_flags_start + 896 + 1024 + 1536) # 1536 bytes for 768 textured quads
		black_tris_flags = other_bytes.slice(polygon_render_flags_start + 896 + 1024 + 1536, polygon_render_flags_start + 896 + 1024 + 1536 + 128) # 128 bytes for 64 untextured triangles
		black_quads_flags = other_bytes.slice(polygon_render_flags_start + 896 + 1024 + 1536 + 128, polygon_render_flags_start + 896 + 1024 + 1536 + 128 + 512) # 512 bytes for 256 untextured quads

	_create_mesh()

	albedo_texture = get_texture_all(texture_bytes)
	mesh_material = StandardMaterial3D.new()
	mesh_material.shading_mode = BaseMaterial3D.SHADING_MODE_UNSHADED
	mesh_material.texture_filter = BaseMaterial3D.TEXTURE_FILTER_NEAREST
	mesh_material.transparency = BaseMaterial3D.TRANSPARENCY_ALPHA_SCISSOR
	mesh_material.vertex_color_use_as_albedo = true
	mesh_material.set_texture(BaseMaterial3D.TEXTURE_ALBEDO, albedo_texture)
	mesh.surface_set_material(0, mesh_material)

	albedo_texture_indexed = get_texture_indexed_all(texture_bytes)

	# https://ffhacktics.com/wiki/Maps/Mesh#Texture_animation_instructions
	# animated texture data
	if texture_animation_instructions_data_start != 0:
		has_texture_animations = true
		texture_animations.resize(32)
		texture_anim_instructions_bytes.resize(32)
		for texture_anim_id: int in 32:
			var texture_animation: TextureAnimationData = TextureAnimationData.new()
			var texture_anim_bytes_start: int = texture_animation_instructions_data_start + (texture_anim_id * 20)
			var texture_anim_instruction_bytes: PackedByteArray = other_bytes.slice(texture_anim_bytes_start, texture_anim_bytes_start + 20)
			texture_anim_instructions_bytes[texture_anim_id] = texture_anim_instruction_bytes

			texture_animation.texture_anim_instruction_bytes = texture_anim_instruction_bytes

			texture_animation.canvas_y = texture_anim_instruction_bytes.decode_u8(2)
			texture_animation.canvas_width = texture_anim_instruction_bytes.decode_u8(4) * 4
			texture_animation.canvas_height = texture_anim_instruction_bytes.decode_u8(6)
			texture_animation.frame1_y = texture_anim_instruction_bytes.decode_u8(10)
			# UV animation: 0x01 repeat loop forward, 0x02 loop ping pong forward <-> backward, 0x05 script command, 0x15 script command
			# palette animation: 0x03 repeat loop forward, 0x04 loop ping pong forward <-> backward, 0x00 script command, 0x13 script command
			texture_animation.anim_technique = texture_anim_instruction_bytes.decode_u8(14)
			texture_animation.num_frames = texture_anim_instruction_bytes.decode_u8(15)
			texture_animation.frame_duration = texture_anim_instruction_bytes.decode_u8(17) # 1/30ths of a second (ie. 2 frames)

			if texture_anim_instruction_bytes.decode_u8(1) == 0x03 and texture_anim_instruction_bytes.decode_u8(9) == 0x03:
				texture_animation.animation_type = 0 # UV animation
				@warning_ignore("integer_division")
				texture_animation.texture_page = texture_anim_instruction_bytes.decode_u8(0) * 4 / 256
				texture_animation.canvas_x = texture_anim_instruction_bytes.decode_u8(0) * 4 % 256
				@warning_ignore("integer_division")
				texture_animation.frame1_texture_page = texture_anim_instruction_bytes.decode_u8(8) * 4 / 256
				texture_animation.frame1_x = texture_anim_instruction_bytes.decode_u8(8) * 4 % 256
			elif (texture_anim_instruction_bytes.decode_u8(1) == 0x00
				and texture_anim_instruction_bytes.decode_u8(2) == 0xe0
				and texture_anim_instruction_bytes.decode_u8(3) == 0x01 ):
				texture_animation.animation_type = 1 # palette animation
				texture_animation.palette_id_to_animate = texture_anim_instruction_bytes.decode_u8(0) >> 4
				texture_animation.animation_starting_index = texture_anim_instruction_bytes.decode_u8(8)

			texture_animations[texture_anim_id] = texture_animation

	if palette_animation_frames_data_start != 0:
		texture_animations_palette_frames.resize(16)
		for palette_frame_id: int in 16:
			var palette_frame_bytes_start: int = palette_animation_frames_data_start + (palette_frame_id * 32)
			var palette_frame_bytes: PackedByteArray = other_bytes.slice(palette_frame_bytes_start, palette_frame_bytes_start + 32)
			palette_animation_bytes.append_array(palette_frame_bytes)
			var palette_frame: PackedColorArray
			palette_frame.resize(16)
			for color_id: int in 16:
				var color_bits: int = palette_frame_bytes.decode_u16(color_id * 2)
				palette_frame[color_id] = color5_to_color8(color_bits)
			texture_animations_palette_frames[palette_frame_id] = palette_frame


func clear_map_data() -> void:
	mesh = null
	mesh_material = null
	albedo_texture = null

	num_text_tris = 0
	num_text_quads = 0
	num_black_tris = 0
	num_black_quads = 0

	text_tri_vertices = []
	text_quad_vertices = []
	black_tri_vertices = []
	black_quad_vertices = []

	text_tri_normals = []
	text_quad_normals = []

	tris_uvs = []
	quads_uvs = []
	tris_palettes = []
	quads_palettes = []

	texture_palettes = []
	texture_color_indices = []

	map_width = 0
	map_length = 0
	terrain_tiles = []


func _create_mesh() -> void:
	st.clear()
	st.begin(Mesh.PRIMITIVE_TRIANGLES)
	# use RGBA format for custom0 even though centroid is Vector3 because data will be in RGBA format when exported/imported as GLTF
	st.set_custom_format(0, SurfaceTool.CUSTOM_RGBA_FLOAT)

	# add textured tris
	for i: int in num_text_tris:
		var v0: Vector3 = text_tri_vertices[i * 3] * SCALE
		var v1: Vector3 = text_tri_vertices[i * 3 + 1] * SCALE
		var v2: Vector3 = text_tri_vertices[i * 3 + 2] * SCALE
		var render_flags: int = convert_render_bitflags(textured_tris_flags.decode_u16(i * 2))
		var centroid: Vector3 = (v0 + v1 + v2) / 3.0
		var centroid_color: Color = Color(centroid.x, centroid.y, centroid.z, float(render_flags))
		for vertex_index: int in 3:
			var index: int = (i * 3) + vertex_index
			st.set_normal(text_tri_normals[index] * SCALE)
			st.set_uv(tris_uvs[index])
			st.set_color(Color.WHITE)
			st.set_custom(0, centroid_color)
			st.add_vertex(text_tri_vertices[index] * SCALE)

	# add black tris
	for i: int in num_black_tris:
		var v0: Vector3 = black_tri_vertices[i * 3] * SCALE
		var v1: Vector3 = black_tri_vertices[i * 3 + 1] * SCALE
		var v2: Vector3 = black_tri_vertices[i * 3 + 2] * SCALE
		var render_flags: int = convert_render_bitflags(black_tris_flags.decode_u16(i * 2))
		var centroid: Vector3 = (v0 + v1 + v2) / 3.0
		var centroid_color: Color = Color(centroid.x, centroid.y, centroid.z, float(render_flags))
		for vertex_index: int in 3:
			var index: int = (i * 3) + vertex_index
			st.set_color(Color.BLACK)
			st.set_custom(0, centroid_color)
			st.add_vertex(black_tri_vertices[index] * SCALE)

	# add textured quads
	for i: int in num_text_quads:
		var quad_start: int = i * 4
		var quad_end: int = (i + 1) * 4
		var quad_vertices: PackedVector3Array = text_quad_vertices.slice(quad_start, quad_end)
		var quad_normals: PackedVector3Array = text_quad_normals.slice(quad_start, quad_end)
		var quad_uvs: PackedVector2Array = quads_uvs.slice(quad_start, quad_end)
		var render_flags: int = convert_render_bitflags(textured_quads_flags.decode_u16(i * 2))
		var centroid: Vector3 = (quad_vertices[0] + quad_vertices[1] + quad_vertices[2] + quad_vertices[3]) * SCALE / 4.0
		var centroid_color: Color = Color(centroid.x, centroid.y, centroid.z, float(render_flags))

		for vert_index: int in [0, 1, 2]:
			st.set_normal(quad_normals[vert_index] * SCALE) # TODO why is there error on MAP105 "terminate"
			st.set_uv(quad_uvs[vert_index])
			st.set_color(Color.WHITE)
			st.set_custom(0, centroid_color)
			st.add_vertex(quad_vertices[vert_index] * SCALE)

		for vert_index: int in [3, 2, 1]:
			st.set_normal(quad_normals[vert_index] * SCALE)
			st.set_uv(quad_uvs[vert_index])
			st.set_color(Color.WHITE)
			st.set_custom(0, centroid_color)
			st.add_vertex(quad_vertices[vert_index] * SCALE)

	# add black quads
	for i: int in num_black_quads:
		var quad_start: int = i * 4
		var quad_end: int = (i + 1) * 4
		var quad_vertices: PackedVector3Array = black_quad_vertices.slice(quad_start, quad_end)
		var render_flags: int = convert_render_bitflags(black_quads_flags.decode_u16(i * 2))
		var centroid: Vector3 = (quad_vertices[0] + quad_vertices[1] + quad_vertices[2] + quad_vertices[3]) * SCALE / 4.0
		var centroid_color: Color = Color(centroid.x, centroid.y, centroid.z, float(render_flags))

		for vert_index: int in [0, 1, 2]:
			st.set_color(Color.BLACK)
			st.set_custom(0, centroid_color)
			st.add_vertex(quad_vertices[vert_index] * SCALE)

		for vert_index: int in [3, 2, 1]:
			st.set_color(Color.BLACK)
			st.set_custom(0, centroid_color)
			st.add_vertex(quad_vertices[vert_index] * SCALE)

	st.generate_tangents()
	mesh = st.commit()


func convert_render_bitflags(render_flags: int) -> int:
	var new_flags: int = 0 # see https://ffhacktics.com/wiki/Maps/Mesh#Polygon_Render_Properties
	if render_flags & (1 << 13) != 0:
		new_flags |= MapData.HiddenDirectionFlag.NORTHEAST
	if render_flags & (1 << 12) != 0:
		new_flags |= MapData.HiddenDirectionFlag.SOUTHEAST
	if render_flags & (1 << 11) != 0:
		new_flags |= MapData.HiddenDirectionFlag.SOUTHWEST
	if render_flags & (1 << 10) != 0:
		new_flags |= MapData.HiddenDirectionFlag.NORTHWEST
	
	return new_flags


func set_mesh_data(primary_mesh_data: PackedByteArray) -> void:
	num_text_tris = primary_mesh_data.decode_u16(0)
	num_text_quads = primary_mesh_data.decode_u16(2)
	num_black_tris = primary_mesh_data.decode_u16(4)
	num_black_quads = primary_mesh_data.decode_u16(6)

	var text_tris_vertices_data_length: int = num_text_tris * 2 * 3 * NUM_VERTICIES_PER_TRI
	var text_quad_vertices_data_length: int = num_text_quads * 2 * 3 * NUM_VERTICIES_PER_QUAD
	var black_tris_vertices_data_length: int = num_black_tris * 2 * 3 * NUM_VERTICIES_PER_TRI
	var black_quads_vertices_data_length: int = num_black_quads * 2 * 3 * NUM_VERTICIES_PER_QUAD
	var tris_uvs_data_length: int = num_text_tris * TEXTURE_BYTES_PER_TRI
	var quad_uvs_data_length: int = num_text_quads * TEXTURE_BYTES_PER_QUAD
	var untextured_polygon_bytes_length: int = (4 * num_black_tris) + (4 * num_black_quads)
	var textured_polygon_tile_bytes_length: int = (2 * num_text_tris) + (2 * num_text_quads)

	var text_quad_vertices_start: int = 8 + text_tris_vertices_data_length
	var black_tris_vertices_start: int = text_quad_vertices_start + text_quad_vertices_data_length
	var black_quads_vertices_start: int = black_tris_vertices_start + black_tris_vertices_data_length
	var text_tri_normals_start: int = black_quads_vertices_start + black_quads_vertices_data_length
	var text_quad_normals_start: int = text_tri_normals_start + text_tris_vertices_data_length
	var tris_uvs_start: int = text_quad_normals_start + text_quad_vertices_data_length
	var quads_uvs_start: int = tris_uvs_start + tris_uvs_data_length
	var untextured_polygon_bytes_start: int = quads_uvs_start + quad_uvs_data_length
	var textured_polygon_tile_bytes_start: int = untextured_polygon_bytes_start + untextured_polygon_bytes_length

	text_tri_vertices = get_vertices(primary_mesh_data.slice(8, text_quad_vertices_start), num_text_tris * NUM_VERTICIES_PER_TRI)
	text_quad_vertices = get_vertices(primary_mesh_data.slice(text_quad_vertices_start, black_tris_vertices_start), num_text_quads * NUM_VERTICIES_PER_QUAD)
	black_tri_vertices = get_vertices(primary_mesh_data.slice(black_tris_vertices_start, black_quads_vertices_start), num_black_tris * NUM_VERTICIES_PER_TRI)
	black_quad_vertices = get_vertices(primary_mesh_data.slice(black_quads_vertices_start, text_tri_normals_start), num_black_quads * NUM_VERTICIES_PER_QUAD)

	text_tri_normals = get_normals(primary_mesh_data.slice(text_tri_normals_start, text_quad_normals_start), num_text_tris * NUM_VERTICIES_PER_TRI)
	text_quad_normals = get_normals(primary_mesh_data.slice(text_quad_normals_start, tris_uvs_start), num_text_quads * NUM_VERTICIES_PER_QUAD)

	#tris_uvs = get_uvs(primary_mesh_data.slice(tris_uvs_start, quads_uvs_start), num_text_tris, false)
	#quads_uvs = get_uvs(primary_mesh_data.slice(quads_uvs_start, quads_uvs_start + quad_uvs_data_length), num_text_quads, true)
	tris_texture_bytes = primary_mesh_data.slice(tris_uvs_start, quads_uvs_start)
	quads_texture_bytes = primary_mesh_data.slice(quads_uvs_start, quads_uvs_start + quad_uvs_data_length)
	tris_uvs = get_uvs_all_palettes(tris_texture_bytes, num_text_tris, false)
	quads_uvs = get_uvs_all_palettes(quads_texture_bytes, num_text_quads, true)

	untextured_polygon_bytes = primary_mesh_data.slice(untextured_polygon_bytes_start, untextured_polygon_bytes_start + untextured_polygon_bytes_length)
	textured_polygon_tile_bytes = primary_mesh_data.slice(textured_polygon_tile_bytes_start, textured_polygon_tile_bytes_start + textured_polygon_tile_bytes_length)


func get_vertices(vertex_bytes: PackedByteArray, num_vertices: int) -> PackedVector3Array:
	var vertices: PackedVector3Array = []

	for vertex_index: int in num_vertices:
		var byte_index: int = vertex_index * 6
		var x: int = vertex_bytes.decode_s16(byte_index)
		var y: int = vertex_bytes.decode_s16(byte_index + 2)
		var z: int = vertex_bytes.decode_s16(byte_index + 4)

		var vertex: Vector3 = Vector3(x, y, z)
		vertices.append(vertex)

	return vertices


func get_normals(normals_bytes: PackedByteArray, num_vertices: int) -> PackedVector3Array:
	var normals: PackedVector3Array = []

	for vertex_index: int in num_vertices:
		var byte_index: int = vertex_index * 6
		var x: float = normals_bytes.decode_s16(byte_index) / 4096.0
		var y: float = normals_bytes.decode_s16(byte_index + 2) / 4096.0
		var z: float = normals_bytes.decode_s16(byte_index + 4) / 4096.0

		var normal: Vector3 = Vector3(x, y, z)
		normals.append(normal)

	return normals


func get_uvs(uvs_bytes: PackedByteArray, num_polys: int, is_quad: bool = false) -> PackedVector2Array:
	var uvs: PackedVector2Array = []

	var data_length: int = 10
	if is_quad:
		data_length = 12

	for poly_index: int in num_polys:
		var byte_index: int = poly_index * data_length

		var texture_page: int = uvs_bytes.decode_u8(byte_index + 6) & 0b11 # two right most bits are texture page
		var v_offset: int = texture_page * 256
		var palette_index: int = uvs_bytes.decode_u8(byte_index + 2)

		# u and v need to be percentage, ie. u / width and v / height
		var au: float = uvs_bytes.decode_u8(byte_index) / 256.0
		var av: float = (uvs_bytes.decode_u8(byte_index + 1) + v_offset) / float(TEXTURE_SIZE.y)
		var bu: float = uvs_bytes.decode_u8(byte_index + 4) / 256.0
		var bv: float = (uvs_bytes.decode_u8(byte_index + 5) + v_offset) / float(TEXTURE_SIZE.y)
		var cu: float = uvs_bytes.decode_u8(byte_index + 8) / 256.0
		var cv: float = (uvs_bytes.decode_u8(byte_index + 9) + v_offset) / float(TEXTURE_SIZE.y)

		var auv: Vector2 = Vector2(au, av)
		var buv: Vector2 = Vector2(bu, bv)
		var cuv: Vector2 = Vector2(cu, cv)
		uvs.append(auv)
		uvs.append(buv)
		uvs.append(cuv)

		if is_quad:
			var du: float = uvs_bytes.decode_u8(byte_index + 10) / 256.0
			var dv: float = (uvs_bytes.decode_u8(byte_index + 11) + v_offset) / float(TEXTURE_SIZE.y)

			var duv: Vector2 = Vector2(du, dv)
			uvs.append(duv)
			quads_palettes.append(palette_index)
		else:
			tris_palettes.append(palette_index)

	return uvs


func get_uvs_all_palettes(uvs_bytes: PackedByteArray, num_polys: int, is_quad: bool = false) -> PackedVector2Array:
	var uvs: PackedVector2Array = []
	var num_palettes: int = 16

	var data_length: int = 10
	if is_quad:
		data_length = 12

	for poly_index: int in num_polys:
		var byte_index: int = poly_index * data_length

		var texture_page: int = uvs_bytes.decode_u8(byte_index + 6) & 0b11 # two right most bits are texture page
		var v_offset: int = texture_page * 256
		var palette_index: int = uvs_bytes.decode_u8(byte_index + 2)
		var x_offset: int = palette_index * 256

		# u and v need to be percentage, ie. u / width and v / height
		var au: float = (uvs_bytes.decode_u8(byte_index) + x_offset) / float(TEXTURE_SIZE.x * num_palettes)
		var av: float = (uvs_bytes.decode_u8(byte_index + 1) + v_offset) / float(TEXTURE_SIZE.y)
		var bu: float = (uvs_bytes.decode_u8(byte_index + 4) + x_offset) / float(TEXTURE_SIZE.x * num_palettes)
		var bv: float = (uvs_bytes.decode_u8(byte_index + 5) + v_offset) / float(TEXTURE_SIZE.y)
		var cu: float = (uvs_bytes.decode_u8(byte_index + 8) + x_offset) / float(TEXTURE_SIZE.x * num_palettes)
		var cv: float = (uvs_bytes.decode_u8(byte_index + 9) + v_offset) / float(TEXTURE_SIZE.y)

		var auv: Vector2 = Vector2(au, av)
		var buv: Vector2 = Vector2(bu, bv)
		var cuv: Vector2 = Vector2(cu, cv)
		uvs.append(auv)
		uvs.append(buv)
		uvs.append(cuv)

		if is_quad:
			var du: float = (uvs_bytes.decode_u8(byte_index + 10) + x_offset) / float(TEXTURE_SIZE.x * num_palettes)
			var dv: float = (uvs_bytes.decode_u8(byte_index + 11) + v_offset) / float(TEXTURE_SIZE.y)

			var duv: Vector2 = Vector2(du, dv)
			uvs.append(duv)
			quads_palettes.append(palette_index)
		else:
			tris_palettes.append(palette_index)

	return uvs


func get_texture_palettes(texture_palettes_bytes: PackedByteArray) -> PackedColorArray:
	var new_texture_palettes: PackedColorArray = []
	var num_colors: int = 256 # 16 palettes of 16 colors each
	new_texture_palettes.resize(num_colors)

	for i: int in num_colors:
		#var color: Color = Color.BLACK
		var color_bits: int = texture_palettes_bytes.decode_u16(i * 2)
		new_texture_palettes[i] = color5_to_color8(color_bits)

	return new_texture_palettes


func color5_to_color8(color_bits: int) -> Color:
	var color: Color = Color.BLACK
	color.a8 = (color_bits & 0b1000_0000_0000_0000) >> 15 # first bit is alpha (if bit is zero, color is transparent)
	color.b8 = (color_bits & 0b0111_1100_0000_0000) >> 10 # then 5 bits each: blue, green, red
	color.g8 = (color_bits & 0b0000_0011_1110_0000) >> 5
	color.r8 = color_bits & 0b0000_0000_0001_1111

	# convert 5 bit channels to 8 bit
	color.a8 = 255 * color.a8 # first bit is alpha (if bit is one, color is opaque)
	#color.a8 = 255 # TODO use alpha correctly?
	color.b8 = roundi(255 * (color.b8 / float(31))) # then 5 bits each: blue, green, red
	color.g8 = roundi(255 * (color.g8 / float(31)))
	color.r8 = roundi(255 * (color.r8 / float(31)))

	# if R == G == B == A == 0, then the color is transparent.
	if (color == Color(0, 0, 0, 0)):
		color.a8 = 0
	#if (i % 16) == 0:
		#color.a8 = 0
	else:
		color.a8 = 255

	return color


func get_texture_color_indices(texture_bytes: PackedByteArray) -> PackedInt32Array:
	var new_color_indicies: PackedInt32Array = []
	var bits_per_pixel: int = 4
	new_color_indicies.resize(texture_bytes.size() * 2)

	for i: int in new_color_indicies.size():
		@warning_ignore("integer_division")
		var pixel_offset: int = (i * bits_per_pixel) / 8
		var byte: int = texture_bytes.decode_u8(pixel_offset)

		if i % 2 == 1: # get 4 leftmost bits
			new_color_indicies[i] = byte >> 4
		else:
			new_color_indicies[i] = byte & 0b0000_1111 # get 4 rightmost bits

	return new_color_indicies


func get_texture_pixel_colors(palette_id: int = 0) -> PackedColorArray:
	var new_pixel_colors: PackedColorArray = []
	var new_size: int = TEXTURE_SIZE.x * TEXTURE_SIZE.y
	new_pixel_colors.resize(new_size)
	new_pixel_colors.fill(Color.BLACK)
	for i: int in new_size:
		new_pixel_colors[i] = texture_palettes[texture_color_indices[i] + (16 * palette_id)]

	return new_pixel_colors


func get_texture_rgba8_image(palette_id: int = 0, pixel_colors: PackedColorArray = []) -> Image:
	var image: Image = Image.create_empty(TEXTURE_SIZE.x, TEXTURE_SIZE.y, false, Image.FORMAT_RGBA8)
	if pixel_colors.is_empty():
		pixel_colors = get_texture_pixel_colors(palette_id)

	for x: int in TEXTURE_SIZE.x:
		for y: int in TEXTURE_SIZE.y:
			var color: Color = pixel_colors[x + (y * TEXTURE_SIZE.x)]
			var color8: Color = Color8(color.r8, color.g8, color.b8, color.a8) # use Color8 function to prevent issues with format conversion changing color by 1/255
			image.set_pixel(x, y, color8) # spr stores pixel data left to right, top to bottm

	return image


func get_texture(texture_bytes: PackedByteArray, palette_id: int = 0) -> Texture2D:
	texture_color_indices = get_texture_color_indices(texture_bytes)

	#var unique_palettes: Dictionary = {}
	#for palette_index: int in tris_palettes:
		#unique_palettes[palette_index] = 1
	#push_warning("Tris palettes: " + str(unique_palettes.keys()))
	#
	#unique_palettes.clear()
	#for palette_index: int in quads_palettes:
		#unique_palettes[palette_index] = 1
	#push_warning("Quads palettes: " + str(unique_palettes.keys()))

	return ImageTexture.create_from_image(get_texture_rgba8_image(palette_id))


func get_texture_indexed_all(texture_bytes: PackedByteArray) -> ImageTexture:
	var num_palettes: int = 16
	var colors_per_palette: int = 16
	var image_width: int = TEXTURE_SIZE.x * num_palettes
	var image_indexed: Image = Image.create_empty(image_width, TEXTURE_SIZE.y, false, Image.FORMAT_RGBA8)
	texture_color_indices = get_texture_color_indices(texture_bytes)

	for x: int in TEXTURE_SIZE.x:
		for y: int in TEXTURE_SIZE.y:
			for palette_id: int in num_palettes:
				var pixel_index: int = (TEXTURE_SIZE.x * y) + x
				var new_color: Color = Color.from_rgba8(texture_color_indices[pixel_index] + (palette_id * colors_per_palette), 0, 0, 255)
				image_indexed.set_pixel(x + (palette_id * TEXTURE_SIZE.x), y, new_color)

	return ImageTexture.create_from_image(image_indexed)


func get_texture_color_indices_all(color_indices: PackedInt32Array) -> PackedInt32Array:
	var new_color_indicies: PackedInt32Array = []
	var num_palettes: int = 16
	var colors_per_palette: int = 16
	
	@warning_ignore("integer_division")
	var num_rows: int = color_indices.size() / TEXTURE_SIZE.x
	for row_index: int in num_rows:
		var row_start_index: int = row_index * TEXTURE_SIZE.x
		var row_end_index: int = row_start_index + TEXTURE_SIZE.x
		var row_indices: PackedInt32Array = color_indices.slice(row_start_index, row_end_index)

		for palette_index: int in num_palettes:
			var row_indices_adjusted: PackedInt32Array = []
			row_indices_adjusted.resize(TEXTURE_SIZE.x)
			row_indices_adjusted.fill(palette_index * colors_per_palette)

			for i: int in TEXTURE_SIZE.x:
				row_indices_adjusted[i] += row_indices[i]

			new_color_indicies.append_array(row_indices_adjusted)

	return new_color_indicies


func get_texture_pixel_colors_all() -> PackedColorArray:
	var new_pixel_colors: PackedColorArray = []
	var num_palettes: int = 16
	var new_size: int = TEXTURE_SIZE.x * TEXTURE_SIZE.y * num_palettes
	new_pixel_colors.resize(new_size)
	new_pixel_colors.fill(Color.BLACK)

	var texture_color_indices_all: PackedInt32Array = get_texture_color_indices_all(texture_color_indices)

	for i: int in new_size:
		new_pixel_colors[i] = texture_palettes[texture_color_indices_all[i]]

	return new_pixel_colors


func get_texture_rgba8_image_all() -> Image:
	var num_palettes: int = 16
	var image_width: int = TEXTURE_SIZE.x * num_palettes
	var image: Image = Image.create_empty(image_width, TEXTURE_SIZE.y, false, Image.FORMAT_RGBA8)
	var pixel_colors: PackedColorArray = get_texture_pixel_colors_all()

	for x: int in image_width:
		for y: int in TEXTURE_SIZE.y:
			var color: Color = pixel_colors[x + (y * image_width)]
			# use Color8 function to prevent issues with format conversion changing color by 1/255
			var color8: Color = Color8(color.r8, color.g8, color.b8, color.a8)
			image.set_pixel(x, y, color8) # spr stores pixel data left to right, top to bottm

	return image


func get_texture_all(texture_bytes: PackedByteArray) -> Texture2D:
	texture_color_indices = get_texture_color_indices(texture_bytes)

	var image: Image = get_texture_rgba8_image_all()
	return ImageTexture.create_from_image(image)


### new_palette should have 16 colors
func get_texture_pixel_colors_new_palette(new_palette: PackedColorArray) -> PackedColorArray:
	var new_pixel_colors: PackedColorArray = []
	var new_size: int = TEXTURE_SIZE.x * TEXTURE_SIZE.y
	new_pixel_colors.resize(new_size)
	new_pixel_colors.fill(Color.BLACK)
	for i: int in new_size:
		new_pixel_colors[i] = new_palette[texture_color_indices[i]]

	return new_pixel_colors


func swap_palette(palette_id: int, new_palette: PackedColorArray, map: MapChunkNodes) -> void:
	var new_pixel_colors: PackedColorArray = get_texture_pixel_colors_new_palette(new_palette)
	var new_color_image: Image = get_texture_rgba8_image(0, new_pixel_colors)

	var new_texture_image: Image = albedo_texture.get_image()
	new_texture_image.blit_rect(new_color_image, Rect2i(Vector2i.ZERO, new_color_image.get_size()), Vector2i(palette_id * TEXTURE_SIZE.x, 0))
	var new_texture: ImageTexture = ImageTexture.create_from_image(new_texture_image)

	var new_mesh_material: Material = mesh.surface_get_material(0)
	new_mesh_material.set_texture(BaseMaterial3D.TEXTURE_ALBEDO, new_texture)
	#mesh.surface_set_material(0, new_mesh_material)
	map.mesh_instance.mesh.surface_set_material(0, new_mesh_material)


func animate_palette(texture_anim: TextureAnimationData, map: MapChunkNodes, anim_fps: float) -> void:
	var frame_id: int = 0
	var dir: int = 1
	var colors_per_palette: int = 16

	var map_shader_material: ShaderMaterial = map.mesh_instance.material_override as ShaderMaterial
	while frame_id < texture_anim.num_frames:
		if not is_instance_valid(map):
			break

		var new_anim_palette_id: int = frame_id + texture_anim.animation_starting_index
		var new_palette: PackedColorArray = texture_animations_palette_frames[new_anim_palette_id]
		var new_texture_palette: PackedColorArray = map_shader_material.get_shader_parameter("palettes_colors")
		for color_id: int in colors_per_palette:
			new_texture_palette[color_id + (texture_anim.palette_id_to_animate * colors_per_palette)] = new_palette[color_id]
		map_shader_material.set_shader_parameter("palettes_colors", new_texture_palette)

		#map.mesh.mesh = mesh
		await Engine.get_main_loop().create_timer(texture_anim.frame_duration / anim_fps).timeout
		if texture_anim.anim_technique == 0x3: # loop forward
			frame_id += dir
			frame_id = frame_id % texture_anim.num_frames
		elif texture_anim.anim_technique == 0x4: # loop back and forth
			if frame_id == texture_anim.num_frames - 1:
				dir = -1
			elif frame_id == 0:
				dir = 1
			frame_id += dir


func animate_uv(texture_anim: TextureAnimationData, map: MapChunkNodes, anim_idx: int, anim_fps: float) -> void:
	var frame_id: int = 0
	var dir: int = 1

	var map_shader_material: ShaderMaterial = map.mesh_instance.material_override as ShaderMaterial
	while frame_id < texture_anim.num_frames:
		if not is_instance_valid(map):
			break

		var frame_idxs: PackedFloat32Array = map_shader_material.get_shader_parameter("frame_idx")
		frame_idxs[anim_idx] = float(frame_id)
		map_shader_material.set_shader_parameter("frame_idx", frame_idxs)

		await Engine.get_main_loop().create_timer(texture_anim.frame_duration / anim_fps).timeout
		if texture_anim.anim_technique == 0x1: # loop forward
			frame_id += dir
			frame_id = frame_id % texture_anim.num_frames
		elif texture_anim.anim_technique == 0x2: # loop back and forth
			if frame_id == texture_anim.num_frames - 1:
				dir = -1
			elif frame_id == 0:
				dir = 1
			frame_id += dir


# https://ffhacktics.com/wiki/Maps/Mesh#Terrain
func get_terrain(terrain_bytes: PackedByteArray) -> Array[TerrainTile]:
	map_width = terrain_bytes.decode_u8(0)
	map_length = terrain_bytes.decode_u8(1)

	var tile_data_length: int = 8
	var new_terrain_tiles: Array[TerrainTile] = []
	new_terrain_tiles.clear()
	for layer: int in [0, 1]:
		for z: int in map_length:
			for x: int in map_width:
				var tile_index: int = x + (z * map_width)
				var tile_data_start: int = 2 + (tile_index * tile_data_length) + (layer * 256 * 8) # each layer has space for 256 tiles, each tile data is 8 bytes
				var tile_data: PackedByteArray = terrain_bytes.slice(tile_data_start, tile_data_start + tile_data_length)

				var tile: TerrainTile = TerrainTile.new()
				# tile.bytes = tile_data
				tile.layer = layer
				tile.location = Vector2i(x, z)
				tile.surface_type_id = tile_data.decode_u8(0) & 0b0011_1111 # right 6 bits are the surface type
				tile.height_bottom = tile_data.decode_u8(2) # For sloped tiles, the height of the bottom of the slope
				tile.depth = tile_data.decode_u8(3) >> 5 # left 3 bits are bepth
				tile.slope_height = tile_data.decode_u8(3) & 0b1_1111 # right 5 bits are difference between the height at the top and the height at the bottom
				tile.slope_type_id = tile_data.decode_u8(4)
				tile.thickness = tile_data.decode_u8(5) & 0b1_1111 # right 5 bits are thickness height below the bottom of slope
				tile.no_stand_select = tile_data.decode_u8(6) >> 7 # leftmost bit, Can Walk/Cursor through this tile but not stand on it or select it.
				tile.shading = (tile_data.decode_u8(6) & 0b0000_1100) >> 2 # 2 bits, Terrain Tile Shading. 0 = Normal, 1 = Dark, 2 = Darker, 3 = Darkest
				tile.no_walk = (tile_data.decode_u8(6) & 0b0000_0010) >> 1 # Can't walk on this tile
				tile.no_cursor = tile_data.decode_u8(6) & 0b1 # rightmost bit, Can't move cursor to this tile

				tile.default_camera_position_id = tile_data.decode_u8(7)

				tile.height_mid = tile.height_bottom + (tile.slope_height / 2.0)

				# slope type
				match tile.slope_type_id:
					0:
						tile.slope_type = TerrainTile.SlopeType.FLAT
					0x52, 0x58, 0x25, 0x85:
						tile.slope_type = TerrainTile.SlopeType.RAMP
					0x41, 0x11, 0x14, 0x44:
						tile.slope_type = TerrainTile.SlopeType.LOW_CORNERS
					0x96, 0x66, 0x69, 0x99:
						tile.slope_type = TerrainTile.SlopeType.HIGH_CORNERS
					_:
						push_warning("error determining slope type id: " + str(tile.location))
				
				if tile.slope_height == 0:
					tile.slope_type = TerrainTile.SlopeType.FLAT
				
				# slope rotation
				match tile.slope_type_id:
					0, 0x85, 0x44, 0x99:
						tile.rotation_degrees = 0.0
					0x58, 0x14, 0x69:
						tile.rotation_degrees = 90.0
					0x25, 0x11, 0x66:
						tile.rotation_degrees = 180.0
					0x52, 0x41, 0x96:
						tile.rotation_degrees = 270.0

				new_terrain_tiles.append(tile)

	return new_terrain_tiles


func set_gradient_colors(gradient_color_bytes: PackedByteArray) -> void:
	var top_red: int = gradient_color_bytes.decode_u8(0)
	var top_green: int = gradient_color_bytes.decode_u8(1)
	var top_blue: int = gradient_color_bytes.decode_u8(2)

	var bot_red: int = gradient_color_bytes.decode_u8(3)
	var bot_green: int = gradient_color_bytes.decode_u8(4)
	var bot_blue: int = gradient_color_bytes.decode_u8(5)

	background_gradient_top = Color8(top_red, top_green, top_blue)
	background_gradient_bottom = Color8(bot_red, bot_green, bot_blue)


func get_associated_files(gns_bytes: PackedByteArray) -> Array[MapFileRecord]:
	var gns_record_length: int = 20
	var new_map_records: Array[MapFileRecord] = []

	if file_name == "MAP053.GNS":
		return handle_map053(gns_bytes)

	var num_files: int = -1
	for temp_file_name: String in RomReader.file_records.keys():
		if temp_file_name.contains(file_name.trim_suffix(".GNS")):
			num_files += 1

	for record_index: int in num_files:
		var record_data: PackedByteArray = gns_bytes.slice(record_index * gns_record_length, (record_index + 1) * gns_record_length)
		var new_map_file_record: MapFileRecord = MapFileRecord.new(record_data)
		new_map_records.append(new_map_file_record)

		if new_map_file_record.file_type_indicator == 0x2e01: # MAP053's mesh data is in the 0x2f01 resource
			primary_mesh_data_record = new_map_file_record
			other_data_record = primary_mesh_data_record
		elif primary_mesh_data_record == null and new_map_file_record.file_type_indicator == 0x2f01:
			primary_mesh_data_record = new_map_file_record
			other_data_record = primary_mesh_data_record

	if is_instance_valid(primary_mesh_data_record):
		for record: MapFileRecord in new_map_records:
			if record.file_type_indicator == 0x1701:
				if record.time_weather == primary_mesh_data_record.time_weather and record.arrangement == primary_mesh_data_record.arrangement:
					primary_texture_record = record

	return new_map_records


func handle_map053(gns_bytes: PackedByteArray) -> Array[MapFileRecord]:
	var gns_record_length: int = 20
	var new_map_records: Array[MapFileRecord] = []

	var num_files: int = -1
	for temp_file_name: String in RomReader.file_records.keys():
		if temp_file_name.contains(file_name.trim_suffix(".GNS")):
			num_files += 1

	for record_index: int in num_files:
		var record_data: PackedByteArray = gns_bytes.slice(record_index * gns_record_length, (record_index + 1) * gns_record_length)
		var new_map_file_record: MapFileRecord = MapFileRecord.new(record_data)
		new_map_records.append(new_map_file_record)

		# MAP053's mesh data is in the 0x2f01 resource
		if new_map_file_record.file_type_indicator == 0x2f01:
			primary_mesh_data_record = new_map_file_record

		# MAP053's palette, lighting, and terrain data is in the 0x2e01 resource
		if new_map_file_record.file_type_indicator == 0x2e01:
			other_data_record = new_map_file_record

	if is_instance_valid(primary_mesh_data_record):
		for record: MapFileRecord in new_map_records:
			if record.file_type_indicator == 0x1701:
				if record.time_weather == primary_mesh_data_record.time_weather and record.arrangement == primary_mesh_data_record.arrangement:
					primary_texture_record = record

	return new_map_records


func get_map_scene(scale: Vector3 = Vector3.ONE, translation: Vector3 = Vector3.ZERO, rotation_degrees: float = 0.0) -> MapChunkNodes:
	# map_scale.y = -1 # vanilla used -y as up
	if not is_initialized:
		init_map()

	var new_map_instance: MapChunkNodes = MapChunkNodes.instantiate()
	new_map_instance.map_data = MapData.init_from_fft_map_data(self)
	new_map_instance.name = unique_name

	var tiles_center: Vector2 = MapData.get_tiles_center(new_map_instance.map_data.terrain_tiles)
	var transformed_mesh: ArrayMesh = get_transformed_mesh(mesh, Vector3(tiles_center.x, 0.0, tiles_center.y), scale, translation, rotation_degrees, true)
	new_map_instance.mesh_instance.mesh = transformed_mesh

	new_map_instance.set_mesh_shader(albedo_texture_indexed, texture_palettes, map_lighting)
	new_map_instance.collision_shape.shape = new_map_instance.mesh_instance.mesh.create_trimesh_shape()

	# # new_map_instance.position = map_position
	# #new_map_instance.global_rotation_degrees = Vector3(0, 0, 0)
	
	# new_map_instance.set_mesh_shader(albedo_texture_indexed, texture_palettes)
	
	# # new_map_instance.play_animations(new_map_data)
	# # new_map_instance.input_event.connect(on_map_input_event)
	
	return new_map_instance


func get_scaled_collision_shape(collision_scale: Vector3) -> ConcavePolygonShape3D:
	var new_collision_shape: ConcavePolygonShape3D = mesh.create_trimesh_shape()
	var faces: PackedVector3Array = new_collision_shape.get_faces()
	for i: int in faces.size():
		faces[i] = faces[i] * collision_scale
	
	#push_warning(faces)
	new_collision_shape.set_faces(faces)
	new_collision_shape.backface_collision = true
	return new_collision_shape


static func get_adjusted_mesh_file(fft_map_data: FftMapData, mirror_quadrants: PackedVector2Array, cropped_rect: Rect2i) -> PackedByteArray:
	var adjusted_map_data: FftMapData = get_adjusted_map_data(fft_map_data, mirror_quadrants, cropped_rect)
	var adjusted_map_mesh_file: PackedByteArray = get_fft_mesh_file(adjusted_map_data)
	return adjusted_map_mesh_file


static func get_fft_mesh_file(fft_map_data: FftMapData) -> PackedByteArray:
	var primary_mesh_data_start: int = 0xc4
	var primary_mesh_bytes: PackedByteArray = []
	var primary_mesh_header: PackedByteArray = []
	primary_mesh_header.resize(8)
	primary_mesh_header.fill(0)
	primary_mesh_header.encode_u16(0, fft_map_data.num_text_tris)
	primary_mesh_header.encode_u16(2, fft_map_data.num_text_quads)
	primary_mesh_header.encode_u16(4, fft_map_data.num_black_tris)
	primary_mesh_header.encode_u16(6, fft_map_data.num_black_quads)

	var primary_mesh_textured_tri_verticies: PackedByteArray = []
	primary_mesh_textured_tri_verticies.resize(fft_map_data.num_text_tris * NUM_VERTICIES_PER_TRI * 3 * 2) # 3 coordinates (x, y, x) per vertex, 2 bytes per coordinate
	for tri_index: int in fft_map_data.num_text_tris:
		for vertex_index: int in NUM_VERTICIES_PER_TRI:
			var total_index: int = (tri_index * NUM_VERTICIES_PER_TRI) + vertex_index
			var vertex_coordinates: Vector3 = fft_map_data.text_tri_vertices[total_index]
			var coordinate_index: int = total_index * 3
			primary_mesh_textured_tri_verticies.encode_s16(coordinate_index * 2, roundi(vertex_coordinates.x))
			primary_mesh_textured_tri_verticies.encode_s16((coordinate_index + 1) * 2, roundi(vertex_coordinates.y))
			primary_mesh_textured_tri_verticies.encode_s16((coordinate_index + 2) * 2, roundi(vertex_coordinates.z))
	
	var primary_mesh_textured_quad_verticies: PackedByteArray = []
	primary_mesh_textured_quad_verticies.resize(fft_map_data.num_text_quads * NUM_VERTICIES_PER_QUAD * 3 * 2) # 3 coordinates (x, y, x) per vertex, 2 bytes per coordinate
	for quad_index: int in fft_map_data.num_text_quads:
		for vertex_index: int in NUM_VERTICIES_PER_QUAD:
			var total_index: int = (quad_index * NUM_VERTICIES_PER_QUAD) + vertex_index
			var vertex_coordinates: Vector3 = fft_map_data.text_quad_vertices[total_index]
			var coordinate_index: int = total_index * 3
			primary_mesh_textured_quad_verticies.encode_s16(coordinate_index * 2, roundi(vertex_coordinates.x))
			primary_mesh_textured_quad_verticies.encode_s16((coordinate_index + 1) * 2, roundi(vertex_coordinates.y))
			primary_mesh_textured_quad_verticies.encode_s16((coordinate_index + 2) * 2, roundi(vertex_coordinates.z))
	
	var primary_mesh_black_tri_verticies: PackedByteArray = []
	primary_mesh_black_tri_verticies.resize(fft_map_data.num_black_tris * NUM_VERTICIES_PER_TRI * 3 * 2) # 3 coordinates (x, y, x) per vertex, 2 bytes per coordinate
	for tri_index: int in fft_map_data.num_black_tris:
		for vertex_index: int in NUM_VERTICIES_PER_TRI:
			var total_index: int = (tri_index * NUM_VERTICIES_PER_TRI) + vertex_index
			var vertex_coordinates: Vector3 = fft_map_data.black_tri_vertices[total_index]
			var coordinate_index: int = total_index * 3
			primary_mesh_black_tri_verticies.encode_s16(coordinate_index * 2, roundi(vertex_coordinates.x))
			primary_mesh_black_tri_verticies.encode_s16((coordinate_index + 1) * 2, roundi(vertex_coordinates.y))
			primary_mesh_black_tri_verticies.encode_s16((coordinate_index + 2) * 2, roundi(vertex_coordinates.z))
	primary_mesh_black_tri_verticies.fill(0)
	
	var primary_mesh_black_quad_verticies: PackedByteArray = []
	primary_mesh_black_quad_verticies.resize(fft_map_data.num_black_quads * NUM_VERTICIES_PER_QUAD * 3 * 2) # 3 coordinates (x, y, x) per vertex, 2 bytes per coordinate
	for quad_index: int in fft_map_data.num_black_quads:
		for vertex_index: int in NUM_VERTICIES_PER_QUAD:
			var total_index: int = (quad_index * NUM_VERTICIES_PER_QUAD) + vertex_index
			var vertex_coordinates: Vector3 = fft_map_data.black_quad_vertices[total_index]
			var coordinate_index: int = total_index * 3
			primary_mesh_black_quad_verticies.encode_s16(coordinate_index * 2, roundi(vertex_coordinates.x))
			primary_mesh_black_quad_verticies.encode_s16((coordinate_index + 1) * 2, roundi(vertex_coordinates.y))
			primary_mesh_black_quad_verticies.encode_s16((coordinate_index + 2) * 2, roundi(vertex_coordinates.z))
	primary_mesh_black_quad_verticies.fill(0)

	var primary_mesh_textured_tri_normals: PackedByteArray = []
	primary_mesh_textured_tri_normals.resize(fft_map_data.num_text_tris * NUM_VERTICIES_PER_TRI * 3 * 2) # 3 coordinates (x, y, x) per vertex, 2 bytes per coordinate
	for tri_index: int in fft_map_data.num_text_tris:
		for vertex_index: int in NUM_VERTICIES_PER_TRI:
			var total_index: int = (tri_index * NUM_VERTICIES_PER_TRI) + vertex_index
			var vertex_normals: Vector3 = fft_map_data.text_tri_normals[total_index]
			var coordinate_index: int = total_index * 3
			primary_mesh_textured_tri_normals.encode_s16(coordinate_index * 2, roundi(vertex_normals.x * 4096.0))
			primary_mesh_textured_tri_normals.encode_s16((coordinate_index + 1) * 2, roundi(vertex_normals.y * 4096.0))
			primary_mesh_textured_tri_normals.encode_s16((coordinate_index + 2) * 2, roundi(vertex_normals.z * 4096.0))

	var primary_mesh_textured_quad_normals: PackedByteArray = []
	primary_mesh_textured_quad_normals.resize(fft_map_data.num_text_quads * NUM_VERTICIES_PER_QUAD * 3 * 2) # 3 coordinates (x, y, x) per vertex, 2 bytes per coordinate
	for quad_index: int in fft_map_data.num_text_quads:
		for vertex_index: int in NUM_VERTICIES_PER_QUAD:
			var total_index: int = (quad_index * NUM_VERTICIES_PER_QUAD) + vertex_index
			var vertex_normals: Vector3 = fft_map_data.text_quad_normals[total_index]
			var coordinate_index: int = total_index * 3
			primary_mesh_textured_quad_normals.encode_s16(coordinate_index * 2, roundi(vertex_normals.x * 4096.0))
			primary_mesh_textured_quad_normals.encode_s16((coordinate_index + 1) * 2, roundi(vertex_normals.y * 4096.0))
			primary_mesh_textured_quad_normals.encode_s16((coordinate_index + 2) * 2, roundi(vertex_normals.z * 4096.0))

	primary_mesh_bytes.append_array(primary_mesh_header)
	primary_mesh_bytes.append_array(primary_mesh_textured_tri_verticies)
	primary_mesh_bytes.append_array(primary_mesh_textured_quad_verticies)
	primary_mesh_bytes.append_array(primary_mesh_black_tri_verticies)
	primary_mesh_bytes.append_array(primary_mesh_black_quad_verticies)
	primary_mesh_bytes.append_array(primary_mesh_textured_tri_normals)
	primary_mesh_bytes.append_array(primary_mesh_textured_quad_normals)
	primary_mesh_bytes.append_array(fft_map_data.tris_texture_bytes)
	primary_mesh_bytes.append_array(fft_map_data.quads_texture_bytes)
	primary_mesh_bytes.append_array(fft_map_data.untextured_polygon_bytes)
	primary_mesh_bytes.append_array(fft_map_data.textured_polygon_tile_bytes)

	# texture palettes - handled in create_map()
	# lighting and gradient - handled in create_map()
	# terrain tiles - handled in get_adjusted_map_data()

	# texture animation instructions
	var full_texture_anim_instruction_bytes: PackedByteArray = []
	for texture_anim_instructions: PackedByteArray in fft_map_data.texture_anim_instructions_bytes:
		full_texture_anim_instruction_bytes.append_array(texture_anim_instructions)

	# palette animation instructions - handled in create_map()
	# texture palettes grayscale - handled in create_map()
	# TODO mesh animation instructions
	# TODO animated meshes 1 - 8

	# polygon render flags
	var polygon_render_flags_bytes: PackedByteArray = []
	polygon_render_flags_bytes.resize(896 + 1024 + 1536 + 128 + 512)
	polygon_render_flags_bytes.fill(0)
	# polygon_render_flags_bytes.append_array(fft_map_data.unknown_render_bytes)
	# polygon_render_flags_bytes.append_array(fft_map_data.textured_tris_flags)
	# polygon_render_flags_bytes.append_array(fft_map_data.textured_quads_flags)
	# polygon_render_flags_bytes.append_array(fft_map_data.black_tris_flags)
	# polygon_render_flags_bytes.append_array(fft_map_data.black_quads_flags)

	# header
	var next_section_start: int = 0
	var header_bytes: PackedByteArray = []
	header_bytes.resize(0xc4)
	header_bytes.fill(0)
	header_bytes.encode_u32(0x40, primary_mesh_data_start)
	next_section_start += primary_mesh_data_start
	
	next_section_start += primary_mesh_bytes.size()
	var texture_palettes_data_start: int = next_section_start
	header_bytes.encode_u32(0x44, texture_palettes_data_start)
	
	next_section_start += fft_map_data.texture_palette_bytes.size()
	var lighting_data_start: int = next_section_start
	header_bytes.encode_u32(0x64, lighting_data_start)
	
	next_section_start += fft_map_data.lighting_and_gradient_bytes.size()
	var terrain_data_start: int = next_section_start
	header_bytes.encode_u32(0x68, terrain_data_start)
	
	next_section_start += fft_map_data.terrain_data_bytes.size()
	var texture_animation_instructions_data_start: int = next_section_start
	if full_texture_anim_instruction_bytes.size() == 0:
		texture_animation_instructions_data_start = 0
	header_bytes.encode_u32(0x6c, texture_animation_instructions_data_start)
	
	next_section_start += full_texture_anim_instruction_bytes.size()
	var palette_animation_frames_data_start: int = next_section_start
	if fft_map_data.palette_animation_bytes.size() == 0:
		palette_animation_frames_data_start = 0
	header_bytes.encode_u32(0x70, palette_animation_frames_data_start)
	
	next_section_start += fft_map_data.palette_animation_bytes.size()
	var palettes_grayscale_start: int = next_section_start
	header_bytes.encode_u32(0x7c, palettes_grayscale_start)
	
	var mesh_animation_instructions_start: int = 0
	header_bytes.encode_u32(0x8c, mesh_animation_instructions_start)

	var animated_mesh_1_start: int = 0
	header_bytes.encode_u32(0x90, animated_mesh_1_start)
	var animated_mesh_2_start: int = 0
	header_bytes.encode_u32(0x94, animated_mesh_2_start)
	var animated_mesh_3_start: int = 0
	header_bytes.encode_u32(0x98, animated_mesh_3_start)
	var animated_mesh_4_start: int = 0
	header_bytes.encode_u32(0x9c, animated_mesh_4_start)
	var animated_mesh_5_start: int = 0
	header_bytes.encode_u32(0xa0, animated_mesh_5_start)
	var animated_mesh_6_start: int = 0
	header_bytes.encode_u32(0xa4, animated_mesh_6_start)
	var animated_mesh_7_start: int = 0
	header_bytes.encode_u32(0xa8, animated_mesh_7_start)
	var animated_mesh_8_start: int = 0
	header_bytes.encode_u32(0xac, animated_mesh_8_start)

	next_section_start += fft_map_data.texture_palette_grayscale_bytes.size()
	var polygon_render_flags_start: int = next_section_start
	header_bytes.encode_u32(0xb0, polygon_render_flags_start)

	# full file
	var mesh_file_bytes: PackedByteArray = []
	mesh_file_bytes.append_array(header_bytes)
	mesh_file_bytes.append_array(primary_mesh_bytes)
	mesh_file_bytes.append_array(fft_map_data.texture_palette_bytes)
	mesh_file_bytes.append_array(fft_map_data.lighting_and_gradient_bytes)
	mesh_file_bytes.append_array(fft_map_data.terrain_data_bytes)
	mesh_file_bytes.append_array(full_texture_anim_instruction_bytes)
	mesh_file_bytes.append_array(fft_map_data.palette_animation_bytes)
	mesh_file_bytes.append_array(fft_map_data.texture_palette_grayscale_bytes)
	# TODO mesh animation instructions
	# TODO animated meshes 1 - 8
	mesh_file_bytes.append_array(polygon_render_flags_bytes)

	return mesh_file_bytes


static func get_adjusted_map_data(original_fft_map_data: FftMapData, quadrants: PackedVector2Array, cropped_rect: Rect2i) -> FftMapData:
	var mirrored_maps: Dictionary[Vector2i, FftMapData] = get_mirrored_expanded_map_data(original_fft_map_data, quadrants, true)

	var new_fft_map_data: FftMapData = get_cropped_map_data(cropped_rect, mirrored_maps)
	return new_fft_map_data


static func get_cropped_map_data(cropped_rect: Rect2i, mirrored_map_data: Dictionary[Vector2i, FftMapData]) -> FftMapData:
	var new_fft_map_data: FftMapData = mirrored_map_data.values()[0].duplicate_deep()
	new_fft_map_data.tris_texture_bytes = []
	new_fft_map_data.quads_texture_bytes = []
	new_fft_map_data.text_tri_vertices = []
	new_fft_map_data.text_tri_normals = []
	new_fft_map_data.text_quad_vertices = []
	new_fft_map_data.text_quad_normals = []
	new_fft_map_data.black_tri_vertices = []
	new_fft_map_data.black_quad_vertices = []
	var original_map_size: Vector2i = Vector2i(new_fft_map_data.map_width, new_fft_map_data.map_length)
	new_fft_map_data.map_width = cropped_rect.size.x
	new_fft_map_data.map_length = cropped_rect.size.y

	var vertex_translation: Vector3 = Vector3(-cropped_rect.position.x, 0, -cropped_rect.position.y) * TILE_SIDE_LENGTH

	var new_map_textured_tri_index: int = 0
	var new_map_black_tri_index: int = 0
	var new_map_textured_quad_index: int = 0
	var new_map_black_quad_index: int = 0

	var textured_tri_tile_bytes: PackedByteArray = []
	var textured_quad_tile_bytes: PackedByteArray = []
	
	for mirrored_map_quadrant: Vector2i in mirrored_map_data.keys():
		var mirrored_map: FftMapData = mirrored_map_data[mirrored_map_quadrant]

		var quadrant_start_position: Vector2i = Vector2i.ZERO # bottom left corner of map
		quadrant_start_position.x = mirrored_map.map_width * mirrored_map_quadrant.x
		quadrant_start_position.y = mirrored_map.map_length * mirrored_map_quadrant.y
		var mirrored_map_rect: Rect2i = Rect2i(quadrant_start_position, Vector2i(original_map_size))
		var cropped_intersection: Rect2i = cropped_rect.intersection(mirrored_map_rect)
		
		# get polygon data
		var cropped_polygon_rect: Rect2i = cropped_intersection.grow_individual(0, 0, 1, 1) # include polygons along bottom and right borders
		cropped_polygon_rect.position = cropped_polygon_rect.position * TILE_SIDE_LENGTH
		cropped_polygon_rect.size = cropped_polygon_rect.size * TILE_SIDE_LENGTH
		
		# add textured tris
		for tri_index: int in mirrored_map.num_text_tris:
			var tri_verticies: Array[Vector3i] = []
			var tri_normals: PackedVector3Array = []
			var polygon_in_bounds: bool = true
			for vertex_index: int in NUM_VERTICIES_PER_TRI:
				var total_index: int = (tri_index * NUM_VERTICIES_PER_TRI) + vertex_index
				var new_vertex: Vector3i = Vector3i(mirrored_map.text_tri_vertices[total_index].round())
				tri_verticies.append(new_vertex)
				tri_normals.append(mirrored_map.text_tri_normals[total_index])

				if not cropped_polygon_rect.has_point(Vector2i(new_vertex.x, new_vertex.z)):
					polygon_in_bounds = false
			
			if not polygon_in_bounds:
				continue

			var centroid: Vector3 = Vector3.ZERO
			for vertex_index: int in NUM_VERTICIES_PER_TRI:
				var new_map_total_index: int = (new_map_textured_tri_index * NUM_VERTICIES_PER_TRI) + vertex_index
				if new_fft_map_data.text_tri_vertices.size() > new_map_total_index - 1:
					new_fft_map_data.text_tri_vertices.resize(new_map_total_index + 1)
					new_fft_map_data.text_tri_normals.resize(new_map_total_index + 1)
				var translated_vertex: Vector3 = Vector3(tri_verticies[vertex_index]) + vertex_translation
				centroid += translated_vertex
				new_fft_map_data.text_tri_vertices[new_map_total_index] = translated_vertex
				new_fft_map_data.text_tri_normals[new_map_total_index] = Vector3(tri_normals[vertex_index])
			new_map_textured_tri_index += 1
			var tri_texture_bytes_start: int = tri_index * TEXTURE_BYTES_PER_TRI
			var polygon_texture_bytes: PackedByteArray = mirrored_map.tris_texture_bytes.slice(tri_texture_bytes_start, tri_texture_bytes_start + TEXTURE_BYTES_PER_TRI)
			new_fft_map_data.tris_texture_bytes.append_array(polygon_texture_bytes)

			# var polygon_tile_bytes: PackedByteArray = [
			# 	mirrored_map.textured_polygon_tile_bytes[tri_index * 2],
			# 	mirrored_map.textured_polygon_tile_bytes[(tri_index * 2) + 1],
			# ]
			centroid = centroid / 3.0
			@warning_ignore("integer_division")
			var polygon_tile_x: int = roundi(centroid.x) / TILE_SIDE_LENGTH
			@warning_ignore("integer_division")
			var polygon_tile_z: int = (roundi(centroid.z) / TILE_SIDE_LENGTH) << 1
			var polygon_tile_bytes: PackedByteArray = [
				polygon_tile_z,
				polygon_tile_x,
			]
			# don't highlight polygon if it's vertical
			if roundi(centroid.x) % TILE_SIDE_LENGTH == 0 or roundi(centroid.z) % TILE_SIDE_LENGTH == 0:
				polygon_tile_bytes = [254, 255]

			textured_tri_tile_bytes.append_array(polygon_tile_bytes)
			
		# add black tris
		for tri_index: int in mirrored_map.num_black_tris:
			var tri_verticies: Array[Vector3i] = []
			var polygon_in_bounds: bool = true
			for vertex_index: int in NUM_VERTICIES_PER_TRI:
				var total_index: int = (tri_index * NUM_VERTICIES_PER_TRI) + vertex_index
				var new_vertex: Vector3i = Vector3i(mirrored_map.black_tri_vertices[total_index].round())
				tri_verticies.append(new_vertex)

				if not cropped_polygon_rect.has_point(Vector2i(new_vertex.x, new_vertex.z)):
					polygon_in_bounds = false
					break
			
			if not polygon_in_bounds:
				continue
			
			for vertex_index: int in NUM_VERTICIES_PER_TRI:
				var new_map_total_index: int = (new_map_black_tri_index * NUM_VERTICIES_PER_TRI) + vertex_index
				if new_fft_map_data.black_tri_vertices.size() > new_map_total_index - 1:
					new_fft_map_data.black_tri_vertices.resize(new_map_total_index + 1)
				new_fft_map_data.black_tri_vertices[new_map_total_index] = Vector3(tri_verticies[vertex_index]) + vertex_translation
			new_map_black_tri_index += 1

		# add textured quads
		for quad_index: int in mirrored_map.num_text_quads:
			var quad_verticies: Array[Vector3i] = []
			var quad_normals: PackedVector3Array = []
			var polygon_in_bounds: bool = true
			for vertex_index: int in NUM_VERTICIES_PER_QUAD:
				var total_index: int = (quad_index * NUM_VERTICIES_PER_QUAD) + vertex_index
				var new_vertex: Vector3i = Vector3i(mirrored_map.text_quad_vertices[total_index].round())
				quad_verticies.append(new_vertex)
				quad_normals.append(mirrored_map.text_quad_normals[total_index])

				if not cropped_polygon_rect.has_point(Vector2i(new_vertex.x, new_vertex.z)):
					polygon_in_bounds = false
			
			if not polygon_in_bounds:
				continue

			var centroid: Vector3 = Vector3.ZERO
			for vertex_index: int in NUM_VERTICIES_PER_QUAD:
				var new_map_total_index: int = (new_map_textured_quad_index * NUM_VERTICIES_PER_QUAD) + vertex_index
				if new_fft_map_data.text_quad_vertices.size() > new_map_total_index - 1:
					new_fft_map_data.text_quad_vertices.resize(new_map_total_index + 1)
					new_fft_map_data.text_quad_normals.resize(new_map_total_index + 1)
				var translated_vertex: Vector3 = Vector3(quad_verticies[vertex_index]) + vertex_translation
				centroid += translated_vertex
				new_fft_map_data.text_quad_vertices[new_map_total_index] = translated_vertex
				new_fft_map_data.text_quad_normals[new_map_total_index] = Vector3(quad_normals[vertex_index])
			new_map_textured_quad_index += 1
			var quad_texture_bytes_start: int = quad_index * TEXTURE_BYTES_PER_QUAD
			var polygon_texture_bytes: PackedByteArray = mirrored_map.quads_texture_bytes.slice(quad_texture_bytes_start, quad_texture_bytes_start + TEXTURE_BYTES_PER_QUAD)
			new_fft_map_data.quads_texture_bytes.append_array(polygon_texture_bytes)

			# var polygon_tile_bytes: PackedByteArray = [
			# 	mirrored_map.textured_polygon_tile_bytes[quad_index * 2],
			# 	mirrored_map.textured_polygon_tile_bytes[(quad_index * 2) + 1],
			# ]
			centroid = centroid / 4.0
			@warning_ignore("integer_division")
			var polygon_tile_x: int = roundi(centroid.x) / TILE_SIDE_LENGTH
			@warning_ignore("integer_division")
			var polygon_tile_z: int = (roundi(centroid.z) / TILE_SIDE_LENGTH) << 1
			var polygon_tile_bytes: PackedByteArray = [
				polygon_tile_z,
				polygon_tile_x,
			]
			# don't highlight polygon if it's vertical
			if roundi(centroid.x) % TILE_SIDE_LENGTH == 0 or roundi(centroid.z) % TILE_SIDE_LENGTH == 0:
				polygon_tile_bytes = [254, 255]
			textured_quad_tile_bytes.append_array(polygon_tile_bytes)

		# add black quads
		for quad_index: int in mirrored_map.num_black_quads:
			var quad_verticies: Array[Vector3i] = []
			var polygon_in_bounds: bool = true
			
			for vertex_index: int in NUM_VERTICIES_PER_QUAD:
				var total_index: int = (quad_index * NUM_VERTICIES_PER_QUAD) + vertex_index
				var new_vertex: Vector3i = Vector3i(mirrored_map.black_quad_vertices[total_index].round())
				quad_verticies.append(new_vertex)

				if not cropped_polygon_rect.has_point(Vector2i(new_vertex.x, new_vertex.z)):
					polygon_in_bounds = false
					break
			
			if not polygon_in_bounds:
				continue
			
			for vertex_index: int in NUM_VERTICIES_PER_QUAD:
				var new_map_total_index: int = (new_map_black_quad_index * NUM_VERTICIES_PER_QUAD) + vertex_index
				if new_fft_map_data.black_quad_vertices.size() > new_map_total_index - 1:
					new_fft_map_data.black_quad_vertices.resize(new_map_total_index + 1)
				new_fft_map_data.black_quad_vertices[new_map_total_index] = Vector3(quad_verticies[vertex_index]) + vertex_translation
			new_map_black_quad_index += 1

		# get terrain data
		var cropped_terrain_rect: Rect2i = cropped_intersection.grow_individual(0, 0, 0, 0) # grow rect so terrain tiles on edge are included
		# cropped_terrain_rect.position = cropped_terrain_rect.position - Vector2i(0, 1) # shift rect so points on the top are not included and so points on the bottom are included
		for layer: int in [0, 1]:
			for z: int in mirrored_map.map_length:
				for x: int in mirrored_map.map_width:
					var relative_position: Vector2i = Vector2i(x, z) + quadrant_start_position
					if cropped_terrain_rect.has_point(relative_position):
						var tile_index: int = x + (z * mirrored_map.map_width)
						var tile_data_start: int = 2 + (tile_index * BYTES_PER_TERRAIN_TILE) + (layer * 256 * BYTES_PER_TERRAIN_TILE) # each layer has space for 256 tiles, each tile data is 8 bytes
						var tile_data: PackedByteArray = mirrored_map.terrain_data_bytes.slice(tile_data_start, tile_data_start + BYTES_PER_TERRAIN_TILE)

						var final_tile_position: Vector2i = relative_position - cropped_rect.position
						var final_tile_index: int = final_tile_position.x + (final_tile_position.y * cropped_rect.size.x)
						var final_tile_byte_start: int = 2 + (final_tile_index * BYTES_PER_TERRAIN_TILE) + (layer * 256 * BYTES_PER_TERRAIN_TILE)

						for byte_index: int in BYTES_PER_TERRAIN_TILE:
							new_fft_map_data.terrain_data_bytes[final_tile_byte_start + byte_index] = tile_data[byte_index]
	new_fft_map_data.terrain_data_bytes.encode_u8(0, new_fft_map_data.map_width)
	new_fft_map_data.terrain_data_bytes.encode_u8(1, new_fft_map_data.map_length)

	var total_tiles: int = new_fft_map_data.map_width * new_fft_map_data.map_length
	if total_tiles > 256:
		push_warning("Total tiles > 256: " + str(new_fft_map_data.map_width) + " x " + str(new_fft_map_data.map_length) + " = " + str(total_tiles))

	new_fft_map_data.num_text_tris = new_map_textured_tri_index
	new_fft_map_data.num_black_tris = new_map_black_tri_index
	new_fft_map_data.num_text_quads = new_map_textured_quad_index
	new_fft_map_data.num_black_quads = new_map_black_quad_index

	if new_fft_map_data.num_text_tris > 360:
		push_warning("Total textured tris > 360: " + str(new_fft_map_data.num_text_tris))
	if new_fft_map_data.num_black_tris > 64:
		push_warning("Total black tris > 64: " + str(new_fft_map_data.num_black_tris))
	if new_fft_map_data.num_text_quads > 710:
		push_warning("Total textured quads > 710: " + str(new_fft_map_data.num_text_quads))
	if new_fft_map_data.num_black_quads > 256:
		push_warning("Total black quads > 256: " + str(new_fft_map_data.num_black_quads))

	new_fft_map_data.untextured_polygon_bytes.resize((4 * new_fft_map_data.num_black_tris) + (4 * new_fft_map_data.num_black_quads))
	new_fft_map_data.untextured_polygon_bytes.fill(0)

	new_fft_map_data.textured_polygon_tile_bytes = []
	new_fft_map_data.textured_polygon_tile_bytes.append_array(textured_tri_tile_bytes)
	new_fft_map_data.textured_polygon_tile_bytes.append_array(textured_quad_tile_bytes)

	return new_fft_map_data


static func get_mirrored_expanded_map_data(original_fft_map_data: FftMapData, quadrants: PackedVector2Array, set_depth_zero: bool = false) -> Dictionary[Vector2i, FftMapData]:
	var mirrored_map_data: Dictionary[Vector2i, FftMapData] = {}
	
	for quadrant: Vector2 in quadrants:
		var adjusted_quadrant: Vector2i = Vector2i(roundi(quadrant.x), roundi(quadrant.y))
		var mirrored_fft_map_data: FftMapData = get_mirror_fft_map_data(original_fft_map_data, adjusted_quadrant, set_depth_zero)
		mirrored_map_data[adjusted_quadrant] = mirrored_fft_map_data
	
	return mirrored_map_data


static func get_mirror_fft_map_data(original_fft_map_data: FftMapData, adjusted_quadrant: Vector2i, set_depth_zero: bool = false) -> FftMapData:
	var mirrored_fft_map: FftMapData = original_fft_map_data.duplicate_deep()
	mirrored_fft_map.terrain_data_bytes.fill(0)

	var mirror_scale: Vector3 = Vector3.ONE
	if adjusted_quadrant.x % 2 != 0:
		mirror_scale.x = -1.0
	if adjusted_quadrant.y % 2 != 0:
		mirror_scale.z = -1.0
	
	var map_translation: Vector3 = Vector3.ZERO
	map_translation.x = adjusted_quadrant.x * mirrored_fft_map.map_width
	map_translation.z = adjusted_quadrant.y * mirrored_fft_map.map_length
	if mirror_scale.x == -1.0:
		map_translation.x = (adjusted_quadrant.x + 1) * mirrored_fft_map.map_width # extra adjustment due to implicit translation due to scaling
	if mirror_scale.z == -1.0:
		map_translation.z = (adjusted_quadrant.y + 1) * mirrored_fft_map.map_length # extra adjustment due to implicit translation due to scaling

	var reverse_winding: bool = false
	if mirror_scale == Vector3(-1.0, 1.0, 1.0) or mirror_scale == Vector3(1.0, 1.0, -1.0):
		reverse_winding = true
	
	# add textured tris
	for tri_index: int in mirrored_fft_map.num_text_tris:
		var texture_bytes_start: int = tri_index * TEXTURE_BYTES_PER_TRI
		var original_texture_bytes: PackedByteArray = mirrored_fft_map.tris_texture_bytes.slice(texture_bytes_start, texture_bytes_start + TEXTURE_BYTES_PER_TRI)

		for vertex_index: int in NUM_VERTICIES_PER_TRI:
			var total_index: int = (tri_index * NUM_VERTICIES_PER_TRI) + vertex_index
			var mirror_index: int = total_index
			if reverse_winding and [0, 2].has(vertex_index):
				mirror_index = (tri_index * NUM_VERTICIES_PER_TRI) + NUM_VERTICIES_PER_TRI - 1 - vertex_index
			mirrored_fft_map.text_tri_vertices[mirror_index] = (original_fft_map_data.text_tri_vertices[total_index] * mirror_scale) + (map_translation * TILE_SIDE_LENGTH)
			mirrored_fft_map.text_tri_normals[mirror_index] = original_fft_map_data.text_tri_normals[total_index] * mirror_scale
			
			# reverse winding UVs
			if reverse_winding and vertex_index == 1:
				mirrored_fft_map.tris_texture_bytes[texture_bytes_start + 8] = original_texture_bytes[0]
				mirrored_fft_map.tris_texture_bytes[texture_bytes_start + 9] = original_texture_bytes[1]
			elif reverse_winding and vertex_index == 2:
				mirrored_fft_map.tris_texture_bytes[texture_bytes_start + 0] = original_texture_bytes[8]
				mirrored_fft_map.tris_texture_bytes[texture_bytes_start + 1] = original_texture_bytes[9]

	# add black tris
	for tri_index: int in mirrored_fft_map.num_black_tris:
		for vertex_index: int in NUM_VERTICIES_PER_TRI:
			var total_index: int = (tri_index * NUM_VERTICIES_PER_TRI) + vertex_index
			var mirror_index: int = total_index
			if reverse_winding and [0, 2].has(vertex_index):
				mirror_index = (tri_index * NUM_VERTICIES_PER_TRI) + NUM_VERTICIES_PER_TRI - 1 - vertex_index
			mirrored_fft_map.black_tri_vertices[mirror_index] = (original_fft_map_data.black_tri_vertices[total_index] * mirror_scale) + (map_translation * TILE_SIDE_LENGTH)

	# add textured quads
	for quad_index: int in mirrored_fft_map.num_text_quads:
		var texture_bytes_start: int = quad_index * TEXTURE_BYTES_PER_QUAD
		var original_texture_bytes: PackedByteArray = mirrored_fft_map.quads_texture_bytes.slice(texture_bytes_start, texture_bytes_start + TEXTURE_BYTES_PER_QUAD)
		
		for vertex_index: int in NUM_VERTICIES_PER_QUAD:
			var total_index: int = (quad_index * NUM_VERTICIES_PER_QUAD) + vertex_index
			var mirror_index: int = total_index
			if reverse_winding and [1, 2].has(vertex_index):
				mirror_index = (quad_index * NUM_VERTICIES_PER_QUAD) + NUM_VERTICIES_PER_QUAD - 1 - vertex_index
			mirrored_fft_map.text_quad_vertices[mirror_index] = (original_fft_map_data.text_quad_vertices[total_index] * mirror_scale) + (map_translation * TILE_SIDE_LENGTH)
			mirrored_fft_map.text_quad_normals[mirror_index] = original_fft_map_data.text_quad_normals[total_index] * mirror_scale

			# reverse winding UVs
			if reverse_winding and vertex_index == 1:
				mirrored_fft_map.quads_texture_bytes[texture_bytes_start + 8] = original_texture_bytes[4]
				mirrored_fft_map.quads_texture_bytes[texture_bytes_start + 9] = original_texture_bytes[5]
			elif reverse_winding and vertex_index == 2:
				mirrored_fft_map.quads_texture_bytes[texture_bytes_start + 4] = original_texture_bytes[8]
				mirrored_fft_map.quads_texture_bytes[texture_bytes_start + 5] = original_texture_bytes[9]

	# add black quads
	for quad_index: int in mirrored_fft_map.num_black_quads:
		for vertex_index: int in NUM_VERTICIES_PER_QUAD:
			var total_index: int = (quad_index * NUM_VERTICIES_PER_QUAD) + vertex_index
			var mirror_index: int = total_index
			if reverse_winding and [1, 2].has(vertex_index):
				mirror_index = (quad_index * NUM_VERTICIES_PER_QUAD) + NUM_VERTICIES_PER_QUAD - 1 - vertex_index
			mirrored_fft_map.black_quad_vertices[mirror_index] = (original_fft_map_data.black_quad_vertices[total_index] * mirror_scale) + (map_translation * TILE_SIDE_LENGTH)

	# mirror terrain_tile data
	for layer: int in [0, 1]:
		for z: int in original_fft_map_data.map_length:
			for x: int in original_fft_map_data.map_width:
				var tile_index: int = x + (z * original_fft_map_data.map_width)
				var tile_data_start: int = 2 + (tile_index * BYTES_PER_TERRAIN_TILE) + (layer * 256 * BYTES_PER_TERRAIN_TILE) # first 2 bytes are width and length, each layer has space for 256 tiles, each tile data is 8 bytes
				var tile_data: PackedByteArray = original_fft_map_data.terrain_data_bytes.slice(tile_data_start, tile_data_start + BYTES_PER_TERRAIN_TILE)

				var mirrored_tile_index: int = tile_index
				var mirrored_x: int = x
				var mirrored_z: int = z
				var slope_type: int = tile_data.decode_u8(4) # https://ffhacktics.com/wiki/Slope_Type
				if mirror_scale.x == -1.0:
					mirrored_x = -x + mirrored_fft_map.map_width - 1
					if slope_type == 0x52:
						slope_type = 0x58
					elif slope_type == 0x58:
						slope_type = 0x52
					elif slope_type == 0x41:
						slope_type = 0x44
					elif slope_type == 0x44:
						slope_type = 0x41
					elif slope_type == 0x11:
						slope_type = 0x14
					elif slope_type == 0x14:
						slope_type = 0x11
					elif slope_type == 0x96:
						slope_type = 0x99
					elif slope_type == 0x99:
						slope_type = 0x96
					elif slope_type == 0x66:
						slope_type = 0x69
					elif slope_type == 0x69:
						slope_type = 0x66

				if mirror_scale.z == -1.0:
					mirrored_z = -z + mirrored_fft_map.map_length - 1
					if slope_type == 0x25:
						slope_type = 0x85
					elif slope_type == 0x85:
						slope_type = 0x25
					elif slope_type == 0x41:
						slope_type = 0x11
					elif slope_type == 0x11:
						slope_type = 0x41
					elif slope_type == 0x44:
						slope_type = 0x14
					elif slope_type == 0x14:
						slope_type = 0x44
					elif slope_type == 0x96:
						slope_type = 0x66
					elif slope_type == 0x66:
						slope_type = 0x96
					elif slope_type == 0x99:
						slope_type = 0x69
					elif slope_type == 0x69:
						slope_type = 0x99
				
				mirrored_tile_index = mirrored_x + (mirrored_z * original_fft_map_data.map_width)

				var new_tile_data: PackedByteArray = tile_data.duplicate()
				# optionally zero out all depth
				if set_depth_zero:
					var depth: int = tile_data.decode_u8(3) >> 5 # left 3 bits are depth
					var height: int = tile_data.decode_u8(2)
					new_tile_data.encode_u8(2, height + depth)
					new_tile_data.encode_u8(3, tile_data.decode_u8(3) & 0x1F) # left 3 bits are depth, set to 0, keep right 5 bits

				# mirror slope type
				new_tile_data.encode_u8(4, slope_type)

				# TODO mirror camera rotate?
				# tile.default_camera_position_id = tile_data.decode_u8(7)

				var new_tile_bytes_start: int = 2 + (mirrored_tile_index * BYTES_PER_TERRAIN_TILE) + (layer * 256 * BYTES_PER_TERRAIN_TILE)
				for byte_index: int in BYTES_PER_TERRAIN_TILE:
					mirrored_fft_map.terrain_data_bytes[new_tile_bytes_start + byte_index] = new_tile_data[byte_index]
	mirrored_fft_map.terrain_data_bytes.encode_u8(0, mirrored_fft_map.map_width)
	mirrored_fft_map.terrain_data_bytes.encode_u8(1, mirrored_fft_map.map_length)
	
	# TODO delete overlapping polygons at seams?

	return mirrored_fft_map


class TextureAnimationData extends Resource:
	var texture_anim_instruction_bytes: PackedByteArray = []
	var animation_type: int = -1 # error
	var canvas_y: int
	var canvas_width: int
	var canvas_height: int
	var frame1_y: int
	# UV animation: 0x01 repeat loop forward, 0x02 loop ping pong forward <-> backward, 0x05 script command, 0x15 script command
	# palette animation: 0x03 repeat loop forward, 0x04 loop ping pong forward <-> backward, 0x00 script command, 0x13 script command
	var anim_technique: int
	var num_frames: int
	var frame_duration: int # 1/30ths of a second (ie. 2 frames)
	var texture_page: int
	var canvas_x: int
	var frame1_texture_page: int
	var frame1_x: int
	var palette_id_to_animate: int
	var animation_starting_index: int
