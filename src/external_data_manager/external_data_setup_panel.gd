class_name ExternalDataSetupPanel
extends PanelContainer

@export var import_button: Button

@export var import_path_line_edit: LineEdit
@export var import_find_button: Button
@export var import_file_dialog: FileDialog

@export var import_progress_ui: Container
@export var import_progress: ProgressBar
@export var import_progress_message: Label

@export var rom_path_line_edit: LineEdit
@export var rom_find_button: Button
@export var rom_file_dialog: FileDialog

@export var destination_path_line_edit: LineEdit
@export var destination_find_button: Button
@export var destination_file_dialog: FileDialog

@export var export_data_button: Button

@export var finished_sound: AudioStream

var _default_export_button_text: String

func _ready() -> void:
	rom_path_line_edit.text_changed.connect(_on_rom_path_selected)
	rom_find_button.pressed.connect(func() -> void: rom_file_dialog.visible = true)
	rom_file_dialog.file_selected.connect(_on_rom_path_selected)
	
	destination_path_line_edit.text_changed.connect(_on_destination_path_selected)
	destination_find_button.pressed.connect(func() -> void: destination_file_dialog.visible = true)
	destination_file_dialog.dir_selected.connect(_on_destination_path_selected)

	export_data_button.pressed.connect(export_data)
	import_button.pressed.connect(func() -> void: GameData.index_data(GameData.external_data_paths["IMPORT_PATH"]))

	_default_export_button_text = export_data_button.text

	GameData.import_progress.connect(update_import_progress)
	GameData.message.connect(show_import_message)
	GameData.data_indexed.connect(func() -> void: visible = false)

	await get_tree().process_frame

	import_path_line_edit.text = GameData.external_data_paths["IMPORT_PATH"]
	rom_path_line_edit.text = GameData.external_data_paths["ROM_PATH"]
	destination_path_line_edit.text = GameData.external_data_paths["EXPORT_PATH"]
	update_export_enabled()


func update_export_enabled() -> void:
	var rom_path_is_valid: bool = FileAccess.file_exists(GameData.external_data_paths["ROM_PATH"])
	var destination_path_is_valid: bool = DirAccess.dir_exists_absolute(GameData.external_data_paths["EXPORT_PATH"])
	
	if rom_path_is_valid and destination_path_is_valid:
		export_data_button.disabled = false
		export_data_button.tooltip_text = "Export data from ROM to selected destination"
	else:
		export_data_button.disabled = true
		export_data_button.tooltip_text = ""

		if not rom_path_is_valid:
			export_data_button.tooltip_text += "Invalid path to ROM"
		if not destination_path_is_valid:
			export_data_button.tooltip_text += "Invalid path to export destination"


func _on_rom_path_selected(path: String) -> void:
	GameData.external_data_paths["ROM_PATH"] = path
	rom_path_line_edit.text = path
	rom_file_dialog.visible = false

	GameData.save_data_paths()
	update_export_enabled()


func _on_destination_path_selected(path: String) -> void:
	destination_path_line_edit.text = path
	destination_file_dialog.visible = false

	var import_path_is_export_path: bool = GameData.external_data_paths["IMPORT_PATH"] == GameData.external_data_paths["EXPORT_PATH"]
	if GameData.external_data_paths["IMPORT_PATH"].is_empty() or import_path_is_export_path:
		import_path_line_edit.text = path
		GameData.external_data_paths["IMPORT_PATH"] = path
	GameData.external_data_paths["EXPORT_PATH"] = path
	
	GameData.save_data_paths()
	update_export_enabled()


func export_data() -> void:
	# TODO progress bar/timings
	export_data_button.disabled = true
	rom_find_button.disabled = true
	destination_find_button.disabled = true
	rom_path_line_edit.editable = false
	destination_path_line_edit.editable = false
	
	RomReader.message.connect(show_export_message)

	show_export_message("Loading ROM...")
	await get_tree().process_frame
	await get_tree().process_frame # waiting for two frames is needed for UI to update?
	RomReader.on_load_rom_dialog_file_selected(GameData.external_data_paths["ROM_PATH"])
	
	var start_time: int = Time.get_ticks_msec()
	await RomReader.export_data(GameData.external_data_paths["EXPORT_PATH"])
	var export_time: String = "(%.2f sec)" % ((Time.get_ticks_msec() - start_time) / 1000.0)
	push_warning("data export complete " + export_time + ": " + GameData.external_data_paths["EXPORT_PATH"])

	RomReader.message.disconnect(show_export_message)
	var audio_result := RomReader.last_audio_export_result
	if audio_result.has("error"):
		export_data_button.text = "Assets exported; audio failed (see tooltip)"
		export_data_button.tooltip_text = audio_result.error
		push_warning("Audio cache export failed: " + audio_result.error)
	elif not audio_result.get("diagnostics", []).is_empty():
		export_data_button.text = "Assets exported; some audio unavailable (see tooltip)"
		export_data_button.tooltip_text = "\n".join(audio_result.diagnostics)
	else:
		export_data_button.text = _default_export_button_text
		export_data_button.tooltip_text = "Audio cache exported. Import assets to refresh the cached catalog."
	
	export_data_button.disabled = false
	rom_find_button.disabled = false
	destination_find_button.disabled = false
	rom_path_line_edit.editable = true
	destination_path_line_edit.editable = true
	
	Utilities.play_audio_one_shot(finished_sound)


func show_export_message(message: String) -> void:
	export_data_button.text = message


func update_import_progress(current_value: int, max_value: int) -> void:
	import_progress.max_value = max_value
	import_progress.value = current_value

	if current_value >= max_value:
		import_progress_ui.visible = false
		import_progress_message.visible = false
		import_progress_message.text = ""
		Utilities.play_audio_one_shot(finished_sound)
	else:
		import_progress_ui.visible = true
		import_progress_message.visible = true


func show_import_message(message: String) -> void:
	import_progress_message.text = message
